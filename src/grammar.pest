WHITESPACE = _{ " " }
COMMENT = _{ "#" ~ (!"\r" ~ !"\n" ~ ANY)* }

decimal_prob = @{ ("0." ~ ASCII_DIGIT*) | ("1." ~ "0"*) | ( "." ~ ASCII_DIGIT+) }
percent_prob = @{  (pre_three | pre_two_one | pre_zero) ~ "%"}
    pre_three = _{ "100" ~ ("." ~ "0"*)? }
    pre_two_one = _{ ASCII_DIGIT{1,2} ~ ("." ~ ASCII_DIGIT*)? }
    pre_zero = _{ "." ~ ASCII_DIGIT+ }

num = @{ ASCII_DIGIT+ }
dice = @{ ASCII_DIGIT* ~ "d" ~ ASCII_DIGIT+ }
ident = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHA | "_" | "-")* }

range_to = { "[" ~ expr ~ ".." ~ expr ~ "]"}
repeats = { "[" ~ expr ~ ";" ~ expr ~ "]"}
seq = { "[" ~ (expr ~ ("," ~ expr)* ~ ","?)? ~ "]" }

parens = _{ "(" ~ expr ~ ")" }

fn_lit = { "\\" ~ ident ~ "->" ~ expr }
fn_call = { (ident | parens)  ~ expr+ }

term = _{ fn_lit | parens | percent_prob | decimal_prob | dice | num | seq | repeats | range_to | fn_call | ident }

op = _{ add | sub | mul | div | le | lt | ge | gt | eq | ne }
   add = { "+" }
   sub = { "-" }
   mul = { "*" }
   div = { "/" }
   le = { "<=" }
   lt = { "<" }
   ge = { ">=" }
   gt = { ">" }
   eq = { "==" }
   ne = { "!=" }

expr = { term ~ (op ~ term)* ~ op? }

assignment = { ident ~ "=" ~ expr }
assignment_with_type = { ident ~ ":" ~ ident ~ "=" ~ expr }

eoi = _{ !ANY }
line = _{ SOI ~ assignment_with_type | assignment | expr ~ eoi }